import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their SHA-256 checksums.
        def filesToDownload = [
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.2/default-arm64-v8a.jar", "sha256": "f3a12589bc882caa165760268d6bb13afbf1c778de459daa00b2cc95b3b75117", "destination": file("$buildDir/v1.2.2/default-arm64-v8a.jar")],
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.2/default-armeabi-v7a.jar", "sha256": "0d970ea7c3d0cdec0fdadcf9f3403b7a51dfc9d1b73c6de60859650f51e44b72", "destination": file("$buildDir/v1.2.2/default-armeabi-v7a.jar")],
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.2/default-x86_64.jar", "sha256": "404277eecc9d9936584f52fcf3dbb07c53fddbc8440db1da1d1ebcfba185fd16", "destination": file("$buildDir/v1.2.2/default-x86_64.jar")],
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.2/default-x86.jar", "sha256": "5d92ef4608f4459b3655555f411de9d4a78de7fee8968b506949b0e5ace37160", "destination": file("$buildDir/v1.2.2/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedSHA256 = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedSHA256 != fileInfo.sha256) {
                    destFile.delete()
                    println "SHA-256 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedSHA256 = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedSHA256 != fileInfo.sha256) {
                    throw new GradleException("SHA-256 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)