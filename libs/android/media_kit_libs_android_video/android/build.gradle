import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their SHA-256 checksums.
        def filesToDownload = [
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.3/default-arm64-v8a.jar", "sha256": "54e7bf0ab9526ecb9316c4ade9c0a945cdb13478873ab699adfd0bc6332ba156", "destination": file("$buildDir/v1.2.3/default-arm64-v8a.jar")],
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.3/default-armeabi-v7a.jar", "sha256": "a323cbd8a208f716103ff5a20b4b8981f091918d7d45edef62da02061993c85e", "destination": file("$buildDir/v1.2.3/default-armeabi-v7a.jar")],
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.3/default-x86_64.jar", "sha256": "ec5ba5dcfe5a604ff5e6d8123f927f5f4aa5fd56f2cc4106b5dacdc528cf3b2a", "destination": file("$buildDir/v1.2.3/default-x86_64.jar")],
            ["url": "https://github.com/Predidit/libmpv-android-video-build/releases/download/v1.2.3/default-x86.jar", "sha256": "ce2f5dd87ef1c8ff5305a3647b38ba6a2be2b824b3b09cfdab60147e2c02ad8e", "destination": file("$buildDir/v1.2.3/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedSHA256 = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedSHA256 != fileInfo.sha256) {
                    destFile.delete()
                    println "SHA-256 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedSHA256 = MessageDigest.getInstance("SHA-256").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedSHA256 != fileInfo.sha256) {
                    throw new GradleException("SHA-256 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)